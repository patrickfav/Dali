apply plugin: 'com.android.application'

Properties localProps = getSigningProperties()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            storeFile file(localProps.getProperty('RELEASE_KEYSTORE_PATH','fillhere'))
            storePassword localProps.getProperty('RELEASE_KEYSTORE_PW','')
            keyAlias localProps.getProperty('RELEASE_KEY_ALIAS','')
            keyPassword localProps.getProperty('RELEASE_KEY_PW','')
        }
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        renderscriptTargetApi rootProject.ext.renderscriptTargetApi
        renderscriptSupportModeEnabled true

        buildConfigField "String", "GIT_REV", "\"" + getGitRev() + "\""
        buildConfigField "String", "GIT_BRANCH", "\"" + getGitBranch() + "\""
        buildConfigField "String", "GIT_DATE", "\"" + getGitCommitDate() + "\""
        buildConfigField "String", "BUILD_NUMBER", "\"" + getCiBuildNumber() + "\""
        buildConfigField "String", "BUILD_DATE", "\"" + getBuildTime() + "\""
        buildConfigField "boolean", "IS_CI_BUILD", "${isCiBuild()}"
    }

    buildTypes {
        debug {
            buildConfigField "String", "BUILD_DATE", "\"\""
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.flavorName}-${variant.buildType.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$rootProject.ext.dependencies.support"
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'at.favre.lib:dali:0.3.+'
    implementation 'at.favre.lib.hood:hood-extended:0.5.1'
//    compile project(path: ':dali')
}
